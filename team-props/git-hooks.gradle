tasks.register('copyGitHooks', Copy) {
    description 'Copies the git hooks from team-props/git-hooks to the .git folder.'
    from("${rootDir}/team-props/git-hooks/") {
        include '**/*.sh'
        rename '(.*).sh', '$1'
    }
    into "${rootDir}/.git/hooks"
}

tasks.register('installGitHooks', Exec) {
    description 'Installs the pre-commit git hooks from team-props/git-hooks.'
    group 'git hooks'
    workingDir rootDir
    commandLine 'chmod'
    args '-R', '+x', '.git/hooks/'
    dependsOn copyGitHooks
    doLast {
        logger.info('Git hook installed successfully.')
    }
}

tasks.register('makeGradlewExecutable', Exec) {
    description "Ensures gradlew script is executable."
    commandLine 'chmod', '+x', "${rootDir}/gradlew"
}